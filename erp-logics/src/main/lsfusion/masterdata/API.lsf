MODULE API;

REQUIRE Security;

NAMESPACE API;

CLASS Category 'API' {
     masterData 'Directories'
}
name 'Name' (Category c) = staticCaption (c) IF c IS Category;
id 'id' (Category c) = staticName (c) IF c IS Category;

access 'Access' = DATA BOOLEAN (UserRole, Category);
access 'Access' (User u, Category c) = TRUE IF GROUP SUM 1 IF access (UserRole r, c) IF has (u, r);

limitAccess 'Limited Access' = DATA BOOLEAN () COMPLEX;

EXTEND FORM securityPolicy
     OBJECTS c = Category
     PROPERTIES limitAccess (), name (c) READONLY, id (c) READONLY, access (ur, c)
;

DESIGN securityPolicy {
     rolePolicyContainer {
         NEW category {
             caption = 'API';
             fill = 1;
             MOVE PROPERTY (limitAccess ());
             MOVE BOX (c);
         }
     }
}

getCategories () {
     EXPORT JSON FROM name (Category c), id (c) WHERE access (currentUser (), c) OR NOT limitAccess ();
} @@ api;
