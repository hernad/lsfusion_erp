MODULE TaxStoreDashboard;

REQUIRE TaxReverse, Store;

NAMESPACE Tax;

addVATRange 'Add rate' (TaxUnit taxUnit) {
     IF range (Tax.taxVAT, taxUnit) THEN {
         NEW rt = Rate {
             range (rt) <- range (Tax.taxVAT, taxUnit);
         }
     } ELSE {
         NEW r = Range {
             tax (r) <- Tax.taxVAT;
             rangeType (r) <- RangeType.units;
             reverse (r) <- TRUE;
             in (r, taxUnit) <- TRUE;
             country (r) <- defaultCountry ();
             NEW rt = Rate {
                 range (rt) <- r;
             }
         }
     }
} TOOLBAR IMAGE 'add.png';

include 'On' = DATA LOCAL BOOLEAN (LegalEntity);
dataInclude 'On' = DATA LOCAL BOOLEAN (TaxUnit);
include 'On' (TaxUnit u) = OVERRIDE dataInclude (u), include (legalEntity [Store] (u));

backgroundInclude (LegalEntity l) =
        RGB (203,203,206) IF NOT include (l) AND (GROUP SUM 1 IF dataInclude (TaxUnit u) AND legalEntity [Store] (u) == l);

addVATRange (NUMERIC [10,5] value, DATE date) {
    IF (GROUP SUM 1 IF include (TaxUnit u)) THEN {
        FOR include (TaxUnit u) DO {
            IF range (Tax.taxVAT, u) THEN {
                NEW rt = Rate {
                    range (rt) <- range (Tax.taxVAT, u);
                    value (rt) <- value;
                    dateFrom (rt) <- date;
                }
            } ELSE {
                NEW r = Range {
                    tax (r) <- Tax.taxVAT;
                    rangeType (r) <- RangeType.units;
                    reverse (r) <- TRUE;
                    in (r, u) <- TRUE;
                    country (r) <- defaultCountry ();
                    NEW rt = Rate {
                        range (rt) <- r;
                        value (rt) <- value;
                        dateFrom (rt) <- date;
                    }
                }
            }
        }
        APPLY;
    } ELSE {
        MESSAGE 'No stores selected';
        beforeCanceled () <- TRUE;
    }
};

FORM storesTaxUnit 'Add rate by company'
    OBJECTS v = NUMERIC [10.5] PANEL
    PROPERTIES value = VALUE (v)
    OBJECTS d = DATE PANEL
    PROPERTIES date = VALUE (d)

    OBJECTS l = LegalEntity
    PROPERTIES (l) include BACKGROUND backgroundInclude (l)
    PROPERTIES (l) READONLY name
    FILTERS isCompany (l)
    
    OBJECTS u = TaxUnit
    PROPERTIES (u) include
    PROPERTIES (u) READONLY description
    FILTERS legalEntity [Store] (u) == l
    // FILTERGROUP store FILTER 'Stores' u IS Store 'F9' DEFAULT
    
    EVENTS ON OK BEFORE addVATRange (v, d)
;
DESIGN storesTaxUnit {
    size = (800, 600);
    OBJECTS {
        NEW top {
            type = CONTAINERH;
            MOVE PROPERTY (value) {caption = 'Rate value';}
            MOVE PROPERTY (date) {caption = 'Valid since';}
        }
        NEW columns {
            type = SPLITH;
            fill = 1;
            MOVE BOX (l);
            MOVE BOX (u);
        }
    }
}

addVATRange 'Add rate by company' () {
    NEWSESSION SHOW storesTaxUnit;
} TOOLBAR IMAGE 'add.png';

deleteVATRange (NUMERIC [10,5] value, DATE date) {
    IF (GROUP SUM 1 IF include (TaxUnit u)) THEN {
        DELETE Rate rt WHERE value (rt) == value AND dateFrom (rt) == date AND (GROUP SUM 1 IF in (rt, TaxUnit u) AND include (u));
        APPLY;
    } ELSE {
        MESSAGE 'No stores selected';
        beforeCanceled () <- TRUE;
    }
};
FORM storesTaxUnitD 'Delete rate by site'
    OBJECTS v = NUMERIC [10.5] PANEL
    PROPERTIES value = VALUE (v)
    OBJECTS d = DATE PANEL
    PROPERTIES date = VALUE (d)

    OBJECTS l = LegalEntity
    PROPERTIES (l) include BACKGROUND backgroundInclude (l)
    PROPERTIES (l) READONLY name
    FILTERS isCompany (l)
    
    OBJECTS u = TaxUnit
    PROPERTIES (u) include
    PROPERTIES (u) READONLY description
    FILTERS legalEntity [Store] (u) == l
    // FILTERGROUP store FILTER 'Stores' u IS Store 'F9' DEFAULT
    
    EVENTS ON OK BEFORE deleteVATRange (v, d)
;
DESIGN storesTaxUnitD {
    size = (800, 600);
    OBJECTS {
        NEW top {
            type = CONTAINERH;
            MOVE PROPERTY (value) {caption = 'Rate value';}
            MOVE PROPERTY (date) {caption = 'Valid since';}
        }
        NEW columns {
            type = SPLITH;
            fill = 1;
            MOVE BOX (l);
            MOVE BOX (u);
        }
    }
}

deleteVATRange 'Delete by enterprise' () {
    NEWSESSION SHOW storesTaxUnitD;
} TOOLBAR IMAGE 'delete.png';

FORM storeTaxUnit 'Tax subjects'
    OBJECTS u = TaxUnit
    PROPERTIES (u) READONLY description
    FILTERGROUP store FILTER '' Stores '' u IS Store '' F9 '' DEFAULT
    LIST TaxUnit OBJECT u
;

FORM VATUnitDashboard 'Enter settlement rate'
    OBJECTS u = TaxUnit PANEL
    PROPERTIES (u) description SELECTOR
    FILTERS u IS Store
        
    OBJECTS rt = Rate
    PROPERTIES (rt) value, dateFrom, DELETE
    PROPERTIES DRAW rt addVATRange (u), addVATRange (), deleteVATRange ()
    FILTERS in (rt, u)
    ORDERS dateFrom (rt)
;

DESIGN VATUnitDashboard {
    PROPERTY (description (u)) {
        caption = 'Shop';
    }
    BOX (rt) {
        caption = 'Calculated VAT Rates';
    }
}

NAVIGATOR {
    saleDashboardNavigator {
        NEW VATUnitDashboard;
    }
}
